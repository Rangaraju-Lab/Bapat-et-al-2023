/*
 * This code iterates over multiple folders, opens the image with label "Crop".
 * User is asked to make a background selection.
 * Average background intensity is calculated from user's selection and subtracted from the image.
 * Output: Background subtracted image. Saved in respective subfolders.
 * Note: Output files have a label "BgS" tagged at the end of filename. 
 * Output files also carry the label "Crop"...
 * Hence, DO NOT run the code twice without deleting the output files generated by previous run.
*/

//Location of Parent folder with each subfolder representing one spine.
Parent = "path-to-raw-images-folder"
folderlist = getFileList(Parent);

//Iterate over all subfolders
for (i = 0; i < lengthOf(folderlist); i++) {
	print(folderlist[i]);
	filelist = getFileList(Parent+folderlist[i]);
	
	//Iterate over each file in subfolder
	for (j = 0; j < lengthOf(filelist); j++) {
		print(filelist[j]);
		
		//Open files containing "Crop" and ask user to trace ROI for Background
		if (indexOf(filelist[j], "Crop") >= 0)  {
			open(Parent+folderlist[i]+filelist[j]);
			name =  File.nameWithoutExtension;
			setTool("rectangle");
			waitForUser("Background Subtraction", "Make Background ROI");
			run("Measure");
			Bg = getResult("Mean", 0);
			close("Results");
			run("Select None");
			run("Subtract...", "value="+Bg);
			resetMinAndMax();
			saveAs("tif", Parent+folderlist[i]+name+"_BgS");
			close("*");
		
		}
	
	}
	
}
